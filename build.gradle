plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
}

apply plugin: 'java'

group = 'com.kysh'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

task copyDomaResources(type: Sync)  {
	from sourceSets.main.resources.srcDirs
	into compileJava.destinationDir
	include 'doma.compile.config'
	include 'META-INF/**/*.sql'
	include 'META-INF/**/*.script'
}

compileJava {
	// Depend on the above task
	dependsOn copyDomaResources
	options.encoding = 'UTF-8'
}

compileTestJava {
	options.encoding = 'UTF-8'
	// Disable the annotation processors during the test run
	options.compilerArgs = ['-proc:none']
}

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.seasar.doma.boot:doma-spring-boot-starter:1.1.1'
	implementation 'org.seasar.doma:doma:2.27.1'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.security:spring-security-test'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.seasar.doma:doma:2.27.1'
	annotationProcessor 'org.projectlombok:lombok'
}

test {
	useJUnitPlatform()
}

//文字化け対策
tasks.withType(AbstractCompile)*.options*.encoding = tasks.withType(GroovyCompile)*.groovyOptions*.encoding = 'UTF-8'